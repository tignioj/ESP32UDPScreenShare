#include <WiFi.h>
#include <WiFiUdp.h>
#include "common.h"
#include "scale_function2.h"
#include "network_config.h"

// 本代码是screen share一种实验：把udp接收线程放进core1，这样做的确提高了网络吞吐率1600 pac/s，但是绘制跟不上了,缓冲区总是爆满，画面撕裂严重。

// ================= WiFi =================
const char* ssid = WIFI_SSID_STR;
const char* password = WIFI_PASSWORD_STR;

WiFiUDP udp;
#define UDP_PORT 8888
// ================= Image =================
#define IMG_W 240

#define RGB_LINE_BATCH 12  // 需要足够大，因为放大后行数可能增加

// ================= Frame Buffer =================
#define FRAME_BUF_COUNT 12

enum BufState {
    BUF_FREE,
    BUF_FILLING,
    BUF_READY,
    BUF_DISPLAYING
};

struct FrameData {
    uint16_t frame_id;// = (header[0] << 8) | header[1];
    uint16_t y_start;
    uint16_t line_count;
    uint16_t lines[IMG_W * RGB_LINE_BATCH];
    volatile BufState state;
    uint32_t arrival_time; // 添加时间戳，用于判断最旧的缓冲区
};

FrameData frameBuf[FRAME_BUF_COUNT];

// ================= DMA =================
uint16_t* dmaBuf[2];
volatile uint8_t dmaSel = 0;

// ================= Stats =================
volatile uint32_t frameCount = 0;
volatile uint32_t dropCount = 0;
volatile uint32_t udpPackets = 0;


// ================= UDP Receiver Task =================
void udpReceiverTask(void* param)
{
    // 增大缓冲区以容纳放大后的数据
    static uint8_t rxBuf[1460];

    while (1) {
        // unsigned long start_receive_time = micros();
        int packetSize = udp.parsePacket();
        if (packetSize <= 0) {
            vTaskDelay(1);
            continue;
        }
        udpPackets++;

        // ------------------ 读 Header ------------------
        uint8_t header[5];
        if (udp.read(header, 5) != 5) {
            udp.flush();
            continue;
        }

        uint16_t frame_id = (header[0] << 8) | header[1];
        uint16_t src_y0 = (header[2] << 8) | header[3];
        uint8_t flags = header[4];

        uint8_t resolution = (flags >> 6) & 0x03; // 0=240,1=180,2=120
        uint8_t color_mode = (flags >> 4) & 0x03; // 0=RGB565,1=RGB332
        uint8_t src_lines = flags & 0x0F;

        if (src_lines == 0 || src_lines > RGB_LINE_BATCH) {
            udp.flush();
            continue;
        }

        bool is_rgb565 = (color_mode == 0);

        // ------------------ 源尺寸 ------------------
        int src_w, src_h;
        switch (resolution) {
            case 0: src_w = src_h = 240; break;
            case 1: src_w = src_h = 180; break;
            case 2: src_w = src_h = 120; break;
            default:
                udp.flush();
                continue;
        }

        // ------------------ 计算接收大小 ------------------
        uint32_t bytes_per_px = is_rgb565 ? 2 : 1;
        uint32_t expect = src_w * src_lines * bytes_per_px;

        // ------------------ 找空 buffer 或最旧的 buffer ------------------
        FrameData* f = nullptr;
        int oldest_idx = -1;
        uint32_t oldest_time = UINT32_MAX;
        
        for (int i = 0; i < FRAME_BUF_COUNT; i++) {
            if (frameBuf[i].state == BUF_FREE) {
                f = &frameBuf[i];
                f->state = BUF_FILLING;
                break;
            }
            
            // 记录最旧的 READY 缓冲区（不覆盖正在使用的缓冲区）
            if (frameBuf[i].state == BUF_READY && frameBuf[i].arrival_time < oldest_time) {
                oldest_time = frameBuf[i].arrival_time;
                oldest_idx = i;
            }
        }

        // 如果没有空闲缓冲区，覆盖最旧的 READY 缓冲区
        if (!f && oldest_idx != -1) {
            f = &frameBuf[oldest_idx];
            f->state = BUF_FILLING;
            // 统计覆盖次数
            dropCount++;
        }
        
        // 如果还是没有找到可用缓冲区，丢弃包
        if (!f) {
            udp.flush();
            continue;
        }

        if (udp.read(rxBuf, expect) != expect) {
            f->state = BUF_FREE;
            continue;
        }

        // =================================================
        //            分辨率统一 → 240 RGB565
        // =================================================
        uint16_t* dst = f->lines;
        int dst_y0 = 0;
        int dst_lines = 0;

        // =============== 240 → 240 ======================
        if (src_w == 240) {
            dst_y0 = src_y0;
            dst_lines = src_lines;
            
            if (is_rgb565) {
                memcpy(dst, rxBuf, 240 * src_lines * 2);
            } else {
                int n = src_w * src_lines;
                uint16_t* d = dst;
                uint8_t* src = rxBuf;
                while (n >= 4) {
                    d[0] = rgb332_to_565_lut[src[0]];
                    d[1] = rgb332_to_565_lut[src[1]];
                    d[2] = rgb332_to_565_lut[src[2]];
                    d[3] = rgb332_to_565_lut[src[3]];
                    src += 4;
                    d   += 4;
                    n   -= 4;
                }   
                while (n--) {
                    *d++ = rgb332_to_565_lut[*src++];
                }
            }
        }
        // =============== 180 → 240 ======================
        else if (src_w == 180) {
            // 计算目标行范围
            dst_y0 = (src_y0 * 240 + 120) / 180;  // 四舍五入
            dst_lines = (src_lines * 240 + 179) / 180; // 向上取整
            // 检查缓冲区是否足够
            if (dst_lines > RGB_LINE_BATCH) {
                f->state = BUF_FREE;
                udp.flush();
                continue;
            }
            if (is_rgb565) {
                scale_180_to_240_rgb565(
                     (uint16_t*) rxBuf,
                    dst,
                    src_lines
                );
            } else {
                scale_180_to_240_rgb332(
                    rxBuf,
                    dst,
                    src_lines
                );
            }
        }
        // =============== 120 → 240 ======================
        else if (src_w == 120) {
            // 计算目标行范围
            dst_y0 = src_y0 * 2;
            dst_lines = src_lines * 2;
            // 确保不超出240边界
            if (dst_y0 + dst_lines > 240) {
                dst_lines = 240 - dst_y0;
            }

            // 检查缓冲区是否足够
            // 检查缓冲区是否足够 - 使用动态计算
            if (dst_lines > RGB_LINE_BATCH) {
                // 如果超出，调整接收到的行数
                int max_src_lines = RGB_LINE_BATCH / 2;
                if (src_lines > max_src_lines) {
                    src_lines = max_src_lines;
                    dst_lines = max_src_lines * 2;
                }
            }

            if (is_rgb565) {
                scale_120_to_240_rgb565((uint16_t*)rxBuf,
                dst,
                src_lines);
            } else {
                scale_120_to_240_rgb332(
                    (uint8_t*)rxBuf,
                    dst,
                    src_lines);
            }
     
        }

        // ------------------ 提交 ------------------
        f->frame_id = frame_id;
        f->y_start = dst_y0;
        f->line_count = dst_lines;
        f->arrival_time = millis(); // 记录到达时间
        f->state = BUF_READY;
        // Serial.printf("udp time %lu\n", micros() - start_receive_time);
    }
}

// ================= Draw Frame =================
void drawFrame() {
    FrameData* f = nullptr;
    uint32_t oldest_time = UINT32_MAX;
    int oldest_idx = -1;

    // 找 READY 的缓冲区中最旧的（先进先出）
    for (int i = 0; i < FRAME_BUF_COUNT; i++) {
        if (frameBuf[i].state == BUF_READY && frameBuf[i].arrival_time < oldest_time) {
            oldest_time = frameBuf[i].arrival_time;
            oldest_idx = i;
        }
    }
    
    if (oldest_idx != -1) {
        f = &frameBuf[oldest_idx];
    }
    
    if (!f) return;

    f->state = BUF_DISPLAYING;

    uint8_t nextDma = dmaSel ^ 1;

    // 等待 DMA 完成
    tft->dmaWait();

    memcpy(
        dmaBuf[nextDma],
        f->lines,
        IMG_W * f->line_count * 2
    );

    tft->startWrite();
    tft->pushImageDMA(
        0,
        f->y_start,
        IMG_W,
        f->line_count,
        dmaBuf[nextDma]
    );
    tft->endWrite();

    dmaSel = nextDma;
    f->state = BUF_FREE;
    frameCount++;
}

// ================= Setup =================
void setup() {
    Serial.begin(115200);
    tft_init();
    // setCpuFrequencyMhz(160); // 省电
    setCpuFrequencyMhz(240); // 发热
    
    tft->initDMA();
    tft->setSwapBytes(true);

    tft->setTextFont(1);      // 明确指定字体
    // 清屏并设置文字
    tft->fillScreen(TFT_BLACK);
    tft->setTextSize(2);
    tft->setTextColor(TFT_WHITE);
    // 显示文字
    tft->println("No network! Please connect to AP:ESP32ScreenShareUDP, then input you 2.4G WiFi and password.");
    tft->setCursor(0, 10);    // ❗ 不要带第三个参数
    
    // WiFi.begin(ssid, password);
    // while (WiFi.status() != WL_CONNECTED) {
    //     delay(300);
    //     Serial.print(".");
    // }
    NetworkConfig::begin();   // ⭐ 一行解决 WiFi / AP / 配网
    Serial.println("\nWiFi connected");

    udp.begin(UDP_PORT);

    for (int i = 0; i < FRAME_BUF_COUNT; i++) {
        frameBuf[i].state = BUF_FREE;
        frameBuf[i].arrival_time = 0;
    }
    init_scale_maps();

    // 分配 DMA 缓冲区
    dmaBuf[0] = (uint16_t*)heap_caps_malloc(
        IMG_W * RGB_LINE_BATCH * 2,
        MALLOC_CAP_DMA
    );
    dmaBuf[1] = (uint16_t*)heap_caps_malloc(
        IMG_W * RGB_LINE_BATCH * 2,
        MALLOC_CAP_DMA
    );

    if (!dmaBuf[0] || !dmaBuf[1]) {
        Serial.println("DMA alloc failed");
        while (1);
    }

    // 创建 UDP 接收任务
    xTaskCreatePinnedToCore(
        udpReceiverTask,
        "udp_rx",
        8192,
        nullptr,
        2,
        nullptr,
        1
    );
    
    tft->fillScreen(TFT_BLACK);
    String wifi_str = WiFi.localIP().toString() + ":8888";
    tft->println(wifi_str);
    tft->setCursor(0,34);    // ❗ 不要带第三个参数
    tft->setTextFont(2);      // 明确指定字体
    tft->setTextSize(1);
    tft->println("ScreenShareUDP v0.0.1, transfer color and scale in udp thread.");
    tft->setCursor(0,74);    // ❗ 不要带第三个参数
    tft->println("Client: https://github.com/tignioj/ESP32UDPScreenShareClient");
}

// ================= Debug Info =================
void printDebugInfo() {
    static uint32_t lastPrint = 0;
    uint32_t now = millis();
    
    if (now - lastPrint > 1000) {
        lastPrint = now;
        
        Serial.printf("内存: %u, ", ESP.getFreeHeap());
        Serial.printf("UDP包/秒: %u, ", udpPackets);
        Serial.printf("丢包数: %u, ", dropCount);
        Serial.printf("显示帧数: %u, ", frameCount);
        
        // 显示缓冲区状态
        Serial.print("缓冲区状态: ");
        for (int i = 0; i < FRAME_BUF_COUNT; i++) {
            switch(frameBuf[i].state) {
                case BUF_FREE: Serial.print("F"); break;
                case BUF_FILLING: Serial.print("I"); break;
                case BUF_READY: Serial.print("R"); break;
                case BUF_DISPLAYING: Serial.print("D"); break;
            }
        }
        Serial.println();
        
        udpPackets = 0;
    }
}

// ================= Loop =================
void loop() {
    // unsigned long start_draw_time = micros();
    drawFrame();
    // Serial.printf("绘制耗时:%lu\n", micros() - start_draw_time);
    printDebugInfo();
}